buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "commons-httpclient:commons-httpclient:3.1"
        classpath "com.github.jruby-gradle:jruby-gradle-jar-plugin:1.2.0"
    }
}

plugins {
    id 'java'
    id 'org.ysb33r.vfs' version '1.0-beta8'
//    id "com.github.jruby-gradle.jar" version "1.2.0"
}

apply plugin: 'com.github.jruby-gradle.jar'

jruby {
    defaultVersion = '9.0.5.0'
}

ext {
    theInternetVersion = '0.49.1'
    theInternetDir = file("${buildDir}/the-internet-${theInternetVersion}")
    downloadDir = file("${buildDir}/download")
}

group = 'org.ysb33r'
version = theInternetVersion
sourceCompatibility = 1.7

repositories {
    jcenter{}
}

dependencies {
    compile "org.jruby:jruby-complete:${jruby.defaultVersion}"
    gems 'rubygems:sinatra:1.4.7'
    gems 'rubygems:sinatra-flash:0.3.0'
    gems 'rubygems:sinatra-contrib:1.4.6'
    gems 'rubygems:zurb-foundation:4.3.2'
    gems 'rubygems:compass:1.0.3'
    gems 'rubygems:faker:1.6.3'
}

task download( type : org.ysb33r.gradle.vfs.tasks.VfsCopy ) {
    from "https://github.com/tourdedave/the-internet/archive/${theInternetVersion}.zip"
    into downloadDir
}

task unpack( type : Copy ) {
    dependsOn download
    from zipTree("${downloadDir}/${theInternetVersion}.zip")
    into buildDir

    // This removes the bundler reference
    filesMatching '*/server.rb', { fcd ->
        fcd.filter { line ->
            (line =~ /require 'bundler/) ? null : line
        }
    }
}

task runApp( type : com.github.jrubygradle.JRubyExec ) {
    script "${theInternetDir}/server.rb"
    workingDir theInternetDir
    configuration 'gems'
    dependsOn unpack
}

jrubyJar {
    initScript library()
    into 'the-internet', {
        from theInternetDir
    }
    from project.sourceSets.main.output
    dependsOn unpack, classes
    configuration 'gems'
}

jar.enabled = false

plugins.withType(JavaPlugin) {
    project.tasks.withType(GroovyCompile) { task ->
        task.sourceCompatibility = project.sourceCompatibility
        task.targetCompatibility = project.targetCompatibility
    }
    project.tasks.withType(JavaCompile) { task ->
        task.sourceCompatibility = project.sourceCompatibility
        task.targetCompatibility = project.targetCompatibility
    }
}

//jrubyJar {
//    // tell the plugin to pack a runnable jar (no bootstrap script)
//    initScript library()
//
//    // Includes the default gems to the jar (can be omitted)
//    defaultGems()
//}